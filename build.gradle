task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

repositories {
    maven { url "http://repo.dotcms.com/artifactory/libs-release" }
    maven { url "http://repo.dotcms.com/artifactory/libs-snapshot-local" }
}

configurations.all {
    // dynamic: e.g. 'version: 2.0+'
    resolutionStrategy.cacheDynamicVersionsFor 1, 'hours'
    // changing: e.g. 'version 2.0.0-SNAPSHOT'
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
}


def usePre3_5Build(){
    logger.info("Building with pre3.5 Ant build system.")
    //Import and apply the dependencies from the dependencies scripts
    apply from: "$rootDir/build-pre3_5-ant.gradle"
}

def usePureGradleBuild(){
    logger.info("Building with Gradle")
    ant.importBuild 'build-gradle-migration.xml'
    apply from: "${rootDir}/core.gradle"
}
logger.info("Build flag: ${project.'com.dotcms.build.use-pre-3_5-build'}")
if(project.'com.dotcms.build.use-pre-3_5-build' == 'true'){
    usePre3_5Build()
} else {
    usePureGradleBuild()
}

defaultTasks 'help', 'tasks'

